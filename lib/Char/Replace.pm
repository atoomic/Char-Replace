# Copyright (c) 2018, cPanel, LLC.
# All rights reserved.
# http://cpanel.net
#
# This is free software; you can redistribute it and/or modify it under the
# same terms as Perl itself. See L<perlartistic>.

package Char::Replace;

use strict;
use warnings;

# ABSTRACT: Perl naive XS character replacement as an alternate to substitute or transliterate


BEGIN {

    # VERSION: generated by DZP::OurPkgVersion

    require XSLoader;
    XSLoader::load(__PACKAGE__);
}

sub build_map {
    my (%pairs) = @_;
    my $MAP = identity_map();
    for my $from ( keys %pairs ) {
        length($from) == 1
            or do { require Carp; Carp::croak("build_map: key must be a single character, got '$from'") };
        $MAP->[ ord($from) ] = $pairs{$from};
    }
    return $MAP;
}

1;

=pod

=encoding utf-8

=head1 SYNOPSIS

Char::Replace sample usage

# EXAMPLE: examples/synopsis.pl

=head1 DESCRIPTION

Char::Replace

XS helpers to perform some basic character replacement on strings.

=over

=item replace: replace (transliterate) one or more ASCII characters

=item replace_inplace: fast in-place 1:1 character replacement (no allocation)

=item trim: remove leading and trailing spaces of a string

=item trim_inplace: in-place whitespace trimming (no allocation)

=back

=head1 Available functions

=head2 $output = replace( $string, $MAP )

Return a new string '$output' using the replacement map provided by $MAP (Array Ref).
Map entries can be:

=over

=item a string (PV) — replaces the character with that string

=item an empty string — deletes the character from the output

=item an integer (IV) — replaces the character with C<chr(value)> (0–255)

=item undef — keeps the original character unchanged

=item a code ref — called with the character as argument; return value is the replacement
(return undef to keep original, empty string to delete)

=back

When the input string has the UTF-8 flag set, multi-byte character sequences
(bytes >= 0x80) are copied through unchanged. The replacement map is only
applied to ASCII bytes (0x00–0x7F), preventing corruption of multi-byte
characters whose continuation bytes might collide with map entries.

B<Performance note>: when all map entries are 1:1 byte replacements (single-char
strings, IVs, or identity), a precomputed 256-byte lookup table fast path is used,
avoiding per-byte SV type dispatch. On long strings this can outperform C<tr///>.

B<Taint safety>: when the input string is tainted (under C<-T> mode), the
returned string is also tainted.

view L</SYNOPSIS> or example just after.

Setting a map entry to an empty string deletes the character from the output:

    $map->[ ord('x') ] = q[];    # delete 'x'
    Char::Replace::replace( "fox", $map ) eq "fo" or die;

Setting a map entry to an integer replaces the character with chr(value):

    $map->[ ord('a') ] = ord('A');  # replace 'a' with 'A'
    Char::Replace::replace( "abc", $map ) eq "Abc" or die;

Setting a map entry to a code ref enables dynamic replacement:

    $map->[ ord('a') ] = sub { uc $_[0] };  # uppercase callback
    Char::Replace::replace( "abc", $map ) eq "Abc" or die;

    # stateful callback
    my $n = 0;
    $map->[ ord('x') ] = sub { ++$n };
    Char::Replace::replace( "xyx", $map ) eq "1y2" or die;

=head2 $map = identity_map()

This is a convenient helper to initialize an ASCII mapping.
It returns an Array Ref, where every character will map to itself by default.

You can then adjust one or several characters.

    my $map = Char::Replace::identity_map();
    $map->[ ord('a') ] = q[XYZ]; # replace 'a' by 'XYZ'

    # replaces all 'a' by 'XYZ'
    Char::Replace::replace( "abcdabcd" ) eq "XYZbcdXYZbcd" or die;

=head2 $map = build_map( char => replacement, ... )

Convenience constructor: takes a hash of single-character keys and their
replacement values, and returns an array ref suitable for C<replace()> or
C<replace_inplace()>. Starts from an identity map, so unmapped characters
pass through unchanged.

    my $map = Char::Replace::build_map(
        'a' => 'AA',
        'd' => '',       # delete
        'x' => ord('X'), # IV
        'z' => sub { uc $_[0] },  # callback
    );
    Char::Replace::replace( "abxd", $map ) eq "AAbX" or die;

Croaks if any key is not exactly one character.

=head2 $count = replace_inplace( $string, $MAP )

Modifies C<$string> in place, applying 1:1 byte replacements from C<$MAP>.
Returns the number of bytes actually changed.

Unlike C<replace()>, this function does B<not> allocate a new string — it
modifies the existing SV buffer directly. This restricts map entries to
single-character replacements only:

=over

=item a single-character string (PV of length 1)

=item an integer (IV) in range 0–255

=item undef — keeps the original character unchanged

=back

Multi-character strings, empty strings (deletion), and code refs will cause a croak.
Use C<replace()> when you need expansion, deletion, or dynamic callbacks.

    my $map = Char::Replace::identity_map();
    $map->[ ord('a') ] = 'A';

    my $str = "abcabc";
    my $n = Char::Replace::replace_inplace( $str, $map );
    # $str is now "AbcAbc", $n is 2

UTF-8 safety applies: multi-byte sequences are skipped, only ASCII bytes
are eligible for replacement.

=head2 $string = trim( $string )

trim removes all trailing and leading characters of a string
Trailing and leading space characters  ' ', '\r', '\n', '\t', '\f' are removed.
A new string is returned.

The removal is performed in XS.
We only need to look at the beginning and end of the string.

The UTF-8 and taint state of a string is preserved.

The UTF-8 state of a string is preserved.

=head2 $count = trim_inplace( $string )

Modifies C<$string> in place, removing leading and trailing whitespace.
Returns the total number of whitespace bytes removed.

Unlike C<trim()>, this function does B<not> allocate a new string — it
modifies the existing SV directly. Uses C<sv_chop()> internally for
efficient leading-whitespace removal.

The same whitespace characters as C<trim()> are recognized:
C<' '>, C<'\r'>, C<'\n'>, C<'\t'>, C<'\f'>.

    my $str = "  hello world  ";
    my $n = Char::Replace::trim_inplace( $str );
    # $str is now "hello world", $n is 4

The UTF-8 state of the string is preserved.

=head1 Benchmarks

=head2 char_replace

# EXAMPLE: examples/benchmark-replace.pl

=head2 trim

# EXAMPLE: examples/benchmark-trim.pl


=head1 Warnings

Be aware, that this software is still in a very alpha state at this stage.
Use it as it, patches are welcome. 

=head1 LICENSE

This software is copyright (c) 2018 by cPanel, Inc.

This is free software; you can redistribute it and/or modify it under the same terms as the Perl 5 programming
language system itself.

=head1 DISCLAIMER OF WARRANTY

BECAUSE THIS SOFTWARE IS LICENSED FREE OF CHARGE, THERE IS NO WARRANTY FOR THE SOFTWARE, TO THE EXTENT PERMITTED BY
APPLICABLE LAW. EXCEPT WHEN OTHERWISE STATED IN WRITING THE COPYRIGHT HOLDERS AND/OR OTHER PARTIES PROVIDE THE
SOFTWARE "AS IS" WITHOUT WARRANTY OF ANY KIND, EITHER EXPRESSED OR IMPLIED, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED
WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE. THE ENTIRE RISK AS TO THE QUALITY AND PERFORMANCE
OF THE SOFTWARE IS WITH YOU. SHOULD THE SOFTWARE PROVE DEFECTIVE, YOU ASSUME THE COST OF ALL NECESSARY SERVICING,
REPAIR, OR CORRECTION.

IN NO EVENT UNLESS REQUIRED BY APPLICABLE LAW OR AGREED TO IN WRITING WILL ANY COPYRIGHT HOLDER, OR ANY OTHER PARTY
WHO MAY MODIFY AND/OR REDISTRIBUTE THE SOFTWARE AS PERMITTED BY THE ABOVE LICENCE, BE LIABLE TO YOU FOR DAMAGES,
INCLUDING ANY GENERAL, SPECIAL, INCIDENTAL, OR CONSEQUENTIAL DAMAGES ARISING OUT OF THE USE OR INABILITY TO USE THE
SOFTWARE (INCLUDING BUT NOT LIMITED TO LOSS OF DATA OR DATA BEING RENDERED INACCURATE OR LOSSES SUSTAINED BY YOU OR
THIRD PARTIES OR A FAILURE OF THE SOFTWARE TO OPERATE WITH ANY OTHER SOFTWARE), EVEN IF SUCH HOLDER OR OTHER PARTY HAS
BEEN ADVISED OF THE POSSIBILITY OF SUCH DAMAGES.

